import edu.wpi.first.gradlerio.GradleRIOPlugin

plugins {
  id "java"
  id "edu.wpi.first.GradleRIO" version "2020.1.2"
  id 'com.google.protobuf' version '0.8.11'
  id "com.diffplug.gradle.spotless" version "3.27.0"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def ROBOT_MAIN_CLASS = "frc.robot.Main"

deploy {
  targets {
    roboRIO("roborio") {
      team = 3681
    }
  }
  artifacts {
    frcJavaArtifact('frcJava') {
      targets << "roborio"
    }
    // Built in artifact to deploy arbitrary files to the roboRIO.
    fileTreeArtifact('frcStaticFileDeploy') {
      // The directory below is the local directory to deploy
      files = fileTree(dir: 'src/main/deploy')
      // Deploy to RoboRIO target, into /home/lvuser/deploy
      targets << "roborio"
      directory = '/home/lvuser/deploy'
    }
  }
}

def includeDesktopSupport = false

repositories {
  mavenCentral()
}

def grpcVersion = "1.26.0"
def protobufVersion = "3.11.1"

dependencies {
  compile("javax.annotation:javax.annotation-api:1.3.2")

  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation "io.grpc:grpc-stub:${grpcVersion}"
  implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
  implementation "io.grpc:grpc-netty:${grpcVersion}"

  implementation wpi.deps.wpilib()
  implementation wpi.deps.vendor.java()
  nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
  nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
  testCompile 'junit:junit:4.12'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  manifest GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}

spotless {
  java {
    target fileTree('.') {
      include '**/*.java'
      exclude 'build/**'
    }

    trimTrailingWhitespace()
    removeUnusedImports()

    googleJavaFormat()
  }
}

jar.dependsOn spotlessJavaCheck

protobuf {
  protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
  plugins {
    grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/grpc'
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
}

